apply plugin: 'com.android.application'

android {
  compileSdkVersion 27
  buildToolsVersion "27.0.3"
  dexOptions {
    javaMaxHeapSize "3g"
  }
  signingConfigs {
    release {
      try {
        storeFile file("/Users/abhishek/Android Dev/Release Keys/fnplus-key.jks")
        storePassword KEYSTORE_PASSWORD
        keyAlias KEYSTORE_ALIAS
        keyPassword KEY_PASSWORD
        v2SigningEnabled true
      } catch (ex) {
        throw new InvalidUserDataException(
            "You should define KEYSTORE_PASSWORD, KEYSTORE_ALIAS and KEY_PASSWORD in gradle.properties." +
                ex)
      }
    }
  }
  defaultConfig {
    applicationId "com.instify.android"
    minSdkVersion 16
    targetSdkVersion 27
    versionCode 12
    versionName "2.0.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
    multiDexEnabled true
  }
  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      versionNameSuffix '-DEBUG'
      // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources false
      versionNameSuffix '-RELEASE'
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE-FIREBASE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'LICENSE.txt'
  }
  compileOptions {
    targetCompatibility JavaVersion.VERSION_1_8
    sourceCompatibility JavaVersion.VERSION_1_8
  }
}

ext {
  firebaseLibVersion = "11.8.0"
  supportLibVersion = "27.0.2"
  espressoVersion = "3.0.1"
  androidTestVersion = "0.5"
  hamcrestVersion = "1.3"
  junitVersion = "4.12"
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  // for App Invites and Dynamic Links
  implementation "com.google.firebase:firebase-appindexing:${firebaseLibVersion}"
  implementation "com.google.firebase:firebase-invites:${firebaseLibVersion}"
  // for cloud messaging i.e sending notifications
  implementation "com.google.firebase:firebase-messaging:${firebaseLibVersion}"
  implementation 'com.android.support:multidex:1.0.2'

  // ADS
  implementation "com.google.firebase:firebase-ads:${firebaseLibVersion}"

  // AUTH
  implementation "com.google.firebase:firebase-auth:${firebaseLibVersion}"
  implementation "com.google.android.gms:play-services-auth:${firebaseLibVersion}"

  // DATABASE
  implementation "com.google.firebase:firebase-firestore:${firebaseLibVersion}"
  implementation 'com.firebaseui:firebase-ui-firestore:3.2.1'
  implementation "com.google.firebase:firebase-database:${firebaseLibVersion}"
  implementation 'com.firebaseui:firebase-ui-database:3.2.1'

  // CRASH
  implementation "com.google.firebase:firebase-crash:${firebaseLibVersion}"

  // IMAGE AND LOADING
  implementation 'de.hdodenhof:circleimageview:2.2.0'
  implementation 'com.android.volley:volley:1.0.0'
  implementation 'pub.devrel:easypermissions:1.0.1'
  implementation 'com.github.bumptech.glide:glide:4.3.1'
  annotationProcessor 'com.github.bumptech.glide:compiler:4.3.1'

  // WEB VIEW
  implementation 'com.thefinestartist:finestwebview:1.2.7'

  // MISC
  implementation 'com.jakewharton:butterknife:8.8.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

  // LOGGING/DEBUG
  implementation 'com.jakewharton.timber:timber:4.6.0'
  implementation "com.google.firebase:firebase-core:${firebaseLibVersion}"
  implementation "com.google.firebase:firebase-config:${firebaseLibVersion}"

  // NETWORK
  implementation 'com.squareup.retrofit2:retrofit:2.3.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
  implementation 'com.facebook.stetho:stetho:1.5.0'
  implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
  implementation 'com.squareup.okhttp3:okhttp:3.9.1'
  implementation 'com.google.code.gson:gson:2.8.2'

  // STORAGE FIREBASE
  implementation "com.google.firebase:firebase-storage:${firebaseLibVersion}"

  // SUPPORTING LIBRARIES
  implementation "com.android.support:appcompat-v7:${supportLibVersion}"
  implementation "com.android.support:design:${supportLibVersion}"
  implementation "com.android.support:support-v4:${supportLibVersion}"
  implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
  implementation "com.android.support:cardview-v7:${supportLibVersion}"
  implementation "com.android.support:support-vector-drawable:${supportLibVersion}"
  implementation "com.android.support.constraint:constraint-layout:1.0.2"

  // UI
  implementation 'com.github.apl-devs:appintro:v4.2.2'
  implementation 'com.github.medyo:android-about-page:1.2.2'
  implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
  implementation 'com.github.vipulasri:timelineview:1.0.5'
  implementation 'com.github.clans:fab:1.6.4'
  implementation 'com.getkeepsafe.taptargetview:taptargetview:1.10.0'

  // TESTING
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"
  androidTestImplementation("com.android.support.test.espresso:espresso-core:${espressoVersion}", {
    exclude group: 'com.android.support', module: 'support-annotations'
  })

  // TEST MEMORY LEAK
  debugImplementation'com.squareup.leakcanary:leakcanary-android:1.5.4'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
  testImplementation'junit:junit:4.12'
}
apply plugin: 'com.google.gms.google-services'